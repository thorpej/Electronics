******************************************************
* Model generated on 27 Apr 2010
* Model format: LTspice
* IXCP10M45 macro model
* External node designations
* Node A -> anode (1)
* Node G -> gate (2)
* Node K -> cathode (3)
*
* G1, R1 & C1 model an idealized op-amp. Aol controls
* its open loop gain. Based on LT opamp.lib.
* The op-amp controls a MOSFET M1, to make the Voltage
* drop across a resistor between G & K equal a Voltage
* reference. The Vref source is made up by G2 & R2.
* It is altered by a fraction of V(A,K) to achieve a
* dynamic resistance of 160k where bias resistor = 300R,
* giving nominally 10mA of Ik.
******************************************************

.subckt 10M45S A G K
+params: Aol=100 GBW=10meg ref=3

M1 A 5 K K MOSFET1
* V1 4 G 3
G1 G 5 4 K {Aol}
R1 5 G 1
C1 5 G {Aol/GBW/6.28318530717959}
G2 G 4 value={(ref+V(A,K)/500)}
R2 4 G 1

.model MOSFET1 VDMOS(Rg=2 Vto=4.85 Rd=1m Rs=1m Rb=1.2m Kp=33
+lambda=0.01 Cgs=1.4n Cgdmin=48p Cgdmax=1.9n Cjo=4n Is=2n Vds=450
+Ron=0.92 Qg=48n)
.ends
